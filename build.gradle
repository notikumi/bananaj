apply plugin: "java"
apply plugin: "maven"
//apply plugin: "maven-publish"
apply plugin: 'signing'

group = 'com.notikumi'
version = '0.6'
archivesBaseName = "bananaj"

description = 'A simple MailChimp API wrapper written in Java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'commons-codec', name: 'commons-codec', version: '1.11'
    compile group: 'net.sourceforge.jexcelapi', name: 'jxl', version: '2.6.12'
    compile group: 'org.json', name: 'json', version: '20180130'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task fatJar(type: Jar) {
    baseName = 'bananaj'
    classifier = "fat"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'bananaj'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A simple MailChimp API wrapper written in Java'
                url 'https://github.com/notikumi/bananaj'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://raw.githubusercontent.com/alexanderwe/bananaj/master/LICENSE.md'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/notikumi/bananaj.git'
                    developerConnection = 'scm:git:git://github.com/notikumi/bananaj.git'
                    url = 'https://github.com/notikumi/bananaj'
                }
                issueManagement {
                    url = 'https://github.com/notikumi/bananaj/issues'
                    system = 'GitHub'
                }

                developers {
                    developer {
                        id 'CanosGandia'
                        name 'David CAnos'
                        email 'davidcanos@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    //sign publishing.publications.mavenJava
    sign configurations.archives
}


